/*
Custom Function: WindowCenterInWindow 
Parameters: ~TL ; ~size
Dependencies: Custom Functions:  WindowInfoSet & WindowInfoClear
========================
v 3.0.1 2024.09 updates:
	parameters nHeight & nWidth replaced with single parameter "~size"
v 2.0.1 2023-04 updates: 
	parameters _nHeight & _nWidth are optional - if empty get(WindowHeight & get(WindowWidth) are used
v 1.0.1 2022-12  Benedick Miller, ben@benlight.net
------------------------

Returns a number to positon a new window, or move a window, for Window Top or Window Left, depending on parameter "T" or "L"

"T" returns a number for the New Window Top position
"L" returns a number for the New Window Left position
optional _nHeight is the window height of the new window, for "T" - if Empty get(WindowHeight) is used
optional _nWidth is the window width of the new window,  for "L" - if Empty get(WindowWidth) is used

* Use the custom function: WindowInfoSet() prior to running this cf to set the following required global variables:

$$_windowHeight
$$_windowWidth
$$_windowTop
$$_windowBottom (for future use)
$$_windowLeft
$$_windowRight

* Use the custom function: WindowInfoClear() - to clear the global variables when done

Usage:
------------------------

Paramters: ~TL ; ~size

    "T" to return a value for WindowTop 
    "L" to return a value for WindowLeft

------------------------

Sample usage in Move/Resize Window:

Child window height 80px all-round smaller than parent, centered:
------------------------
Height: ( $$_windowHeight - 80 )
Width: ( $$_windowWidth - 80 )
Window Top: WindowCenterInWindow ( "T" ; $$_windowHeight - 80  ) 
Window Left: WindowCenterInWindow ( "L" ; $$_windowWidth - 80 ) 


Child window, height & width defined, centered:
------------------------
Height: ( 400 )
Width: ( 500 )
Window Top: WindowCenterInWindow ( "T" ; 400  ) 
Window Left: WindowCenterInWindow ( "L" ; 500 )

Child window, usning get(WindowHeight) & Get(WinodWidth), centered:
------------------------
Window Top: WindowCenterInWindow ( "T" ; ""  ) 
Window Left: WindowCenterInWindow ( "L" ; "" ) 

*/

Let ([

	~size = Case ( 
		~TL = "T" ; 
			If (IsEmpty ( ~size ) ; Get(WindowHeight) ; GetAsNumber( ~size )) ;
		 ~TL = "L" ;  
			If (IsEmpty ( ~size ) ; Get(WindowWidth) ; GetAsNumber( ~size ))  ; 
		"" )

];

		Case( 
                         IsEmpty ( $$_windowHeight ) ; "" ;

			~TL = "T" ; 
				Case ( ~size > Get(ScreenHeight) ; 
				0 ; 
				( $$_windowTop + ( $$_windowHeight / 2 )) - ( ~size / 2 )) ;
		 	   ~TL = "L" ;
				( $$_windowLeft + ( $$_windowWidth / 2 )) - ( ~size / 2 )  ;
			"" )
	
)
